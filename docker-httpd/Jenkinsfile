def templatePath = 'docker-httpd/cfg/docker-httpd-template.yaml'
def templateName = 'docker-httpd'
def repoUrl = 'https://github.com/martineg/ocp-labs'

pipeline {
    agent any
    options {
        skipDefaultCheckout()
        durabilityHint('PERFORMANCE_OPTIMIZED')
    }
    stages {
        stage('Init and checkout') {
            steps {
                echo 'Setup'
                git repoUrl
                stash includes: 'docker-httpd/**', name: 'src'
            }
        }

        stage('Cleanup') {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.selector("all", [ app : templateName ]).delete()
                    }
                }
            }
        }

        stage('Apply template') {
            steps {
                unstash 'src'
                script {
                    openshift.withCluster() {
                        openshift.newApp(templatePath)
                    }
                }
            }
        }

        stage('Build') {
            steps {
                script {
                    openshift.withCluster() {
                        def builds = openshift.selector("bc", templateName).related('builds')
                        timeout(5) {
                            builds.untilEach(1) {
                                return (it.object().status.phase == "Complete")
                            }
                        }
                    }
                }
            }
        } 

        stage('Deploy to DEV') {
            steps {
                // implicit deploy on image change
                echo 'Deploy to DEV environment'
                script {
                    openshift.withCluster() {
                        def latestDeploymentVersion = openshift.selector('dc',"${templateName}").object().status.latestVersion
                        def rc = openshift.selector('rc', "${templateName}-${latestDeploymentVersion}")
                        rc.untilEach(1) {
                            def rcMap = it.object()
                            return (rcMap.status.replicas.equals(rcMap.status.readyReplicas))
                        }
                    }
                }
                echo 'Deployment to DEV completed'
            }
        }

        stage('Smoketest in DEV') {
            steps {
                sh '''#! /bin/sh -xe
                sleep 5s
                route=$(oc get route docker-httpd -o jsonpath=\'{.spec.host}\')
                curl -vsS $route | grep -q "Hello OCP"
                '''
            }
        }

        stage('Tag for TEST') {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.tag("${templateName}:latest", "${templateName}:test")
                    }
                }
            }
        }
    }
}